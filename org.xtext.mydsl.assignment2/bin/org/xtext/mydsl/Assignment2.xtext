grammar org.xtext.mydsl.Assignment2 with org.eclipse.xtext.common.Terminals

generate assignment2 "http://www.xtext.org/mydsl/Assignment2"

Model:
	mathexp+=MathExp*
;

MathExp:
	'result' (name=STRING)? 'is' exp=Exp
;

Exp returns Expression:
	Factor (('+' {Plus.left=current} | '-' {Minus.left=current}) right=Factor)*
;

Factor returns Expression:
	Primary (('*' {Mult.left=current} | '/' {Div.left=current}) right=Primary)*	
;

Primary returns Expression:
	Number | Parenthesis | VariableBinding | VariableUse
;

VariableUse returns Expression:
	{Var} id=ID
;

//VariableBinding returns Expression:
//	{Let} 'let' id=ID '=' binding=Exp 'in' body=Exp 'end'
//;

VariableBinding returns Expression:
	{Let} 'let' variable+=Variable ('and' variable+=Variable)* 'in' body=Exp 'end'
;

Variable:
	id=ID '=' binding=Exp
;


Parenthesis returns Expression:
	'(' Exp ')'
;

Number returns Expression:
	{Num} value=INT
;